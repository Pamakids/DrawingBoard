<?xml version="1.0" encoding="utf-8"?>
<s:Group xmlns:fx="http://ns.adobe.com/mxml/2009"
		 xmlns:s="library://ns.adobe.com/flex/spark" width="1024" height="768"
		 xmlns:views="views.*" xmlns:mx="library://ns.adobe.com/flex/mx"
		 xmlns:photos="views.user.photos.*" xmlns:theme="views.theme.*"
		 xmlns:draw="views.draw.*" xmlns:user="views.user.*"
		 xmlns:gallery="views.gallery.*" xmlns:test="views.main.test.*"
		 xmlns:ns="http://flex.apache.org/experimental/ns"
		 xmlns:controls="com.pamakids.components.controls.*"
		 xmlns:components="views.components.*"
		 xmlns:layout="com.pamakids.layout.*"
		 creationComplete="group1_creationCompleteHandler(event)"
		 addedToStage="group1_addedToStageHandler(event)"
		 initialize="group1_initializeHandler(event)" xmlns:main="views.main.*" xmlns:shop="views.shop.*">

	<fx:Script>
		<![CDATA[
			import com.pamakids.services.QNService;

			import mx.collections.ArrayCollection;
			import mx.events.FlexEvent;

			import components.Recommends;

			import controllers.DBPopUp;
			import controllers.MainController;

			import events.DetailEvent;
			import events.DrawEvent;
			import events.ReplayEvent;
			import events.ThemeChangeEvent;
			import events.ThemeFolderEvent;
			import events.UserItemEvent;

			import models.FontManager;
			import models.PaintVO;
			import models.PasterManager;
			import models.ShopManager;
			import models.ThemeFolderVo;
			import models.ThemeManager;
			import models.ThemeVo;
			import models.UserVO;

			import proxy.LoginProxy;

			import views.draw.DrawView;
			import views.gallery.DiscussView;
			import views.gallery.GalleryViewOnline;
			import views.player.ReplayView;
			import views.user.UserCenterView;

			import vo.VO;

			protected function parent_clickHandler(event:MouseEvent):void
			{
				DBPopUp.addGusturePopUp(openParent);
			}

			private function initPopup():void
			{
				popupLayer.graphics.beginFill(0, .6)
				popupLayer.graphics.drawRect(0, 0, 1024, 768);
				popupLayer.graphics.endFill();
				DBPopUp.root=popupLayer;
			}

			private function initFolderList():void
			{
				foldersList.dataProvider=new ArrayCollection(ThemeManager.getInstance().getThemeList());
				foldersList.selectedIndex=0;
				foldersList.visible=true;
			}

			private function initListeners():void
			{
				addEventListener(ThemeFolderEvent.EVENT_ID, onThemeFolder);
				addEventListener("backToMain", onBackToMain);
				addEventListener("gotoGallery", onGotoGallery);
				addEventListener("gotoUserCenter", onGotoUserCenter);

				guestView.addEventListener("exitVisit", onExitVisit);

				addEventListener(ThemeChangeEvent.EVENT_ID,onThemeChange);
				addEventListener(DrawEvent.EVENT_ID, onDraw);
				addEventListener(ReplayEvent.EVENT_ID, onReplay);
				addEventListener(DetailEvent.EVENT_ID, onDetail);
				addEventListener(UserItemEvent.EVENT_ID, onViewGuest);
			}

			private function onThemeChange(e:ThemeChangeEvent):void
			{
				crtTheme=e.data;
				deleteBtn.enabled=crtTheme.online;
			}

			private var crtTheme:ThemeFolderVo;

			protected function onExitVisit(e:Event):void
			{
				if (preState)
				{
					currentState=preState;
					preState='';
				}
				else
					currentState=uiView.name;
			}

			protected function onThemeFolder(e:ThemeFolderEvent):void
			{
				var data:ThemeFolderVo=e.data as ThemeFolderVo;
				themelist.setData(data);

				currentState=uiView.name;
				usercenter.visible=false;
				themelist.visible=true;
				gallery.visible=false;
				shop.visible=false;

				removeDetail();
			}

			private function onBackToMain(e:Event):void
			{
				currentState=mainView.name;
			}

			private function onGotoGallery(e:Event):void
			{
				addGallery();
			}

			private function onGotoUserCenter(e:Event):void
			{
				addUsercenter();
			}

			protected function group1_creationCompleteHandler(event:FlexEvent):void
			{
				if (Multitouch.supportsTouchEvents)
					Multitouch.inputMode=MultitouchInputMode.TOUCH_POINT;

				currentState=visitView.name;
				currentState=uiView.name;
				currentState=mainView.name;

				initListeners();

				initPopup();
				initFolderList();
				initRecommend();
//				initSound();
			}

			private function initRecommend():void
			{
				recommendHodler.addChild(new Recommends(768, 1024));
			}

			public function initSound():void
			{
				var snd:Sound=new Sound(new URLRequest("assets/data/sounds/cnlet.mp3"));
				snd.play();
			}

			private function onDraw(e:DrawEvent):void
			{
				preState=currentState;
				currentState=drawView.name;

				var vo:ThemeVo=e.data;
				drawerLayer.removeAllElements();
				if (db)
				{
					db.dispose();
					db=null;
				}

				db=new DrawView();
				db.initData(vo);
				db.addEventListener("exitDraw", onExitDraw);
				drawerLayer.addElement(db);
			}

			protected function onExitDraw(e:Event):void
			{
				if (e)
				{
					if (preState)
						currentState=preState;
					else
						currentState=mainView.name;
					preState='';
				}
				if (db)
				{
					drawerLayer.removeElement(db);
					db.dispose();
				}
				db=null;
			}

			private var db:DrawView;

			private function openParent():void
			{
				if (!pv)
				{
					pv=new ParentView();
					pv.addEventListener("parentsClose", function(e:Event):void {
						mainGroup.removeElement(pv);
					});
				}
				mainGroup.addElement(pv);
			}

			private var pv:ParentView;

			protected function gallery_clickHandler(event:MouseEvent):void
			{
				addGallery();
			}


			protected function usercenter_clickHandler(event:MouseEvent):void
			{
				addUsercenter();
			}

			private function addGallery():void
			{
				currentState=uiView.name;
				usercenter.visible=false;
				themelist.visible=false;
				gallery.visible=true;
				shop.visible=false;

				removeDetail();
			}

			private function removeDetail():void
			{
				if (detailView)
				{
					if (uiLayer.containsElement(detailView))
						uiLayer.removeElement(detailView);
					detailView.dispose();
					detailView=null;
				}
			}

			private function addUsercenter():void
			{
				currentState=uiView.name;
				usercenter.visible=true;
				themelist.visible=false;
				gallery.visible=false;
				usercenter.user=UserVO.crtUser;
				usercenter.refreshNavi();
			}

			protected function group1_initializeHandler(event:FlexEvent):void
			{
				initConfig();
				LoginProxy.autoLogin();
				FontManager.initialize();

				ThemeManager.getInstance().initialize();
				PasterManager.getInstance().initialize();
				ShopManager.instance.init();
			}

			private function initConfig():void
			{
				MainController.mv=this;
				QNService.HOST=VO.FILESERVER_HOST;
			}

			protected function onReplay(e:ReplayEvent):void
			{
				if (!preState)
					preState=currentState;
				currentState=drawView.name;

				onExitDraw(null);

				if (player)
				{
					player.removeEventListener("replayToCenter", onplayerToCenter);
					player.removeEventListener("closeReplay", onClosePlayer);
					if (drawerLayer.containsElement(player))
						drawerLayer.removeElement(player);
					player.dispose();
				}
				player=null;

				player=new ReplayView();
				player.initData(e.cover, e.data, e.audio,e.id);
				player.addEventListener("replayToCenter", onplayerToCenter);
				player.addEventListener("closeReplay", onClosePlayer);
				player.recordVisible=e.recordVisible;
				drawerLayer.addElement(player);

				if (e.recordVisible)
				{
					usercenter.refreshNavi();
				}
			}

			private var player:ReplayView;
			private var detailView:DiscussView;

			protected function onplayerToCenter(event:Event):void
			{
				removeDetail();
				addUsercenter();
			}

			protected function onClosePlayer(event:Event):void
			{
				if (player)
				{
					player.removeEventListener("replayToCenter", onplayerToCenter);
					player.removeEventListener("closeReplay", onClosePlayer);
					drawerLayer.removeElement(player);
					player.dispose();
				}
				player=null;

				if (preState)
				{
					currentState=preState;
					preState='';
				}
				else
					currentState=mainView.name;
			}

			protected function onDetail(e:DetailEvent):void
			{
				var paint:PaintVO=e.paint;

				removeDetail();
				detailView=new DiscussView();
				detailView.setData(paint);
				uiLayer.addElementAt(detailView, uiLayer.numElements - 1);
				detailView.addEventListener("close", function(e:Event):void {
					uiLayer.removeElement(detailView);
					detailView.dispose();
				});

				detailView.addEventListener("startDraw", function(e:Event):void {
					var vo:ThemeVo=detailView.getTheme();
					drawerLayer.removeAllElements();
					if (db)
					{
						db.dispose();
						db=null;
					}

					db=new DrawView();
					db.initData(vo);
					db.addEventListener("exitDraw", onExitDraw);
					drawerLayer.addElement(db);
				});
			}

			protected function group1_addedToStageHandler(event:Event):void
			{
				stage.quality=StageQuality.LOW;
			}

			protected function onViewGuest(e:UserItemEvent):void
			{
				var user:UserVO=e.user;
				if (UserVO.crtUser && user._id == UserVO.crtUser._id)
				{
					addUsercenter();
				}
				else
				{
					if (!preState)
						preState=currentState;
					currentState=visitView.name;
					guestView.isGuest=true;
					guestView.user=user;
					guestView.refreshNavi();
				}
			}

			private var preState:String;

			protected function shopClick(event:MouseEvent):void
			{
				currentState=uiView.name;
				usercenter.visible=false;
				themelist.visible=false;
				gallery.visible=false;
				shop.visible=true;
				shop.refresh();

				removeDetail();
			}

			public function refreshList():void
			{
				callLater(initFolderList);
			}

			protected function deleteBtn_clickHandler(event:MouseEvent):void
			{
				if(crtTheme&&crtTheme.online)
				{
					DBPopUp.addDeletePopup(crtTheme);
				}
			}

		]]>
	</fx:Script>

	<s:states>
		<mx:State id="mainView" name="mainViewState"/>
		<mx:State id="uiView" name="uiViewState"/>
		<mx:State id="visitView" name="visitViewState"/>
		<mx:State id="drawView" name="drawViewState"/>
	</s:states>

	<s:Group width="100%" height="100%" id="mainGroup" includeIn="mainViewState">
		<s:Image width="100%" height="100%" top="0" left="0"
				 source="@Embed('assets/homepage/img/common/homepageBg_B.png')"
				 mouseEnabled="false"/>

		<s:Image source="@Embed('assets/homepage/img/common/titleBg.png')"
				 mouseEnabled="false"/>

		<s:Image source="@Embed('assets/homepage/img/common/parents.png')"
				 left="16" top="21" click="parent_clickHandler(event)"/>

		<s:Image source="@Embed('assets/global/usercenterBtn.png')" right="42"
				 top="21" click="usercenter_clickHandler(event)"/>

		<s:Image source="@Embed('assets/homepage/img/common/title.png')"
				 top="62" left="368" mouseEnabled="false"/>

		<theme:ThemeFolderList2 id="foldersList" width="100%" height="400"
								top="188" visible="false">
		</theme:ThemeFolderList2>

		<s:Image source="@Embed('assets/homepage/btn/btn_removeTheme.png')" id="deleteBtn"
				 left="330" top="633" enabled="false" click="deleteBtn_clickHandler(event)"/>
		<s:Image source="@Embed('assets/global/galleryBtn.png')" left="512"
				 top="633" click="gallery_clickHandler(event)"/>
		<s:Image source="@Embed('assets/homepage/btn/btn_shop.png')" left="634"
				 top="633" click="shopClick(event)"/>
	</s:Group>

	<mx:UIComponent id="recommendHodler"
					visible="{currentState=='mainViewState'}"/>

	<s:Group id="uiLayer" includeIn="uiViewState">
		<theme:ThemeListView id="themelist" visible="false"/>
		<shop:ShopView id="shop" visible="false"/>
		<gallery:GalleryViewOnline id="gallery" visible="false"/>
		<user:UserCenterView id="usercenter" visible="false"/>
	</s:Group>

	<user:UserCenterView id="guestView" includeIn="visitViewState"/>

	<s:Group width="100%" height="100%" id="drawerLayer"
			 includeIn="drawViewState"/>

	<mx:UIComponent id="popupLayer" width="1024" height="768" visible="false"/>

</s:Group>
